*syntax.txt*    For Vim version 5.0.  Last modification: 1998 Feb 14


		  VIM REFERENCE MANUAL    by Bram Moolenaar


Syntax highlighting		*syntax* *syntax-highlighting* *coloring*

Syntax highlighting enables the possibility to show parts of the text in
another font or color.  Those parts can be specific keywords or text
matching a pattern.  Vim doesn't parse the whole file (to keep it fast), so
the highlighting has its limitations.  Lexical highlighting might be a
better name, but everybody calls it syntax highlighting, so we'll stick with
that.

Vim supports syntax highlighting on all terminals.  But since most ordinary
terminals have very limited highlighting possibilities, it works best in the
GUI version, gvim.

1.  Quick start		 |:syn-qstart|
2.  Syntax files	 |:syn-files|
3.  Syntax file remarks	 |:syn-file-remarks|
4.  Defining a syntax	 |:syn-define|
5.  :syntax arguments	 |:syn-arguments|
6.  Syntax patterns	 |:syn-pattern|
7.  Synchronizing	 |:syn-sync|
8.  Listing syntax items |:syntax|
9.  Highlight command	 |:highlight|
10. Linking groups	 |:highlight-link|
11. Cleaning up		 |:syn-clear|
12. Highlighting tags	 |tag-highlight|
13. Color xterms	 |xterm-color|

{Vi does not have any of these commands}

The syntax highlighting is not available when the |+syntax| feature has been
disabled at compile time.

==============================================================================
1. Quick start						*:syn-qstart*

							*:syn-on*
For a large number of common languages syntax files have been included.  To
start using them, type this command:
>  :syntax on

This will enable automatic syntax highlighting.  The type of highlighting will
be selected using the file name extension, and sometimes using the first line
of the file.

Include this command in your .vimrc if you always want syntax highlighting, or
put it in your .gvimrc if you only want it in the GUI.  If you don't want it
for B&W terminals, but you do want it for color terminals, put this in your
.vimrc:
>  if &t_Co > 1
>    syntax on
>  endif

What this command actually does, is executing the command
>  source $VIM/syntax/syntax.vim
If the VIM environment variable is not set, Vim will try to find
the path in another way (see |$VIM|).  Normally this will work just fine.  If
it doesn't, try setting the VIM environment variable to the directory where
the Vim stuff is located.  For example, if your syntax files are in the
"/usr/vim/5.0/syntax" directory, set $VIM to "/usr/vim/5.0".  You must do this
in the shell, before starting Vim.

							*:syn-default-override*
You can override the default highlight settings, by issuing ":highlight"
commands after sourcing "syntax.vim".  For example:
>  syntax on
>  highlight Constant gui=NONE guibg=grey95

This will change the GUI highlighting for the "Constant" group.  See
|:highlight| about how to specify highlighting attributes.

If you are running in the GUI, you can get white text on a black background
with:
>  highlight Normal guibg=Black guifg=White

If you have a black background, use these commands to get better colors (see
'background'):
>  set background=dark
>  syntax on

NOTE: The syntax files on MS-DOS and Windows have lines that end in <CR><NL>.
The files for Unix end in <NL>.  This means you should use the right type of
file for your system.  Although on MS-DOS and Windows the right format is
automatically selected if the 'fileformats' option is not empty.

To switch off the syntax highlighting:			*:syn-off*
>  :syntax off

You can toggle the syntax on/off with this command
>  :if has("syntax_items") | syntax off | else | syntax on | endif

To put this into a mapping, you can use:
>  map <F7> :if has("syntax_items") <Bar> syntax off <Bar> else <Bar> syntax on <Bar> endif <CR>
[using the |<>| notation, type this literally]

==============================================================================
2. Syntax files						*:syn-files*

The syntax and highlighting commands for one language are normally stored in
a syntax file.  The name convention is: "{name}.vim".  Where {name} is the
name of the language, or an abbreviation (to fit the name in 8.3 characters,
which is always done, in case the file will be used on a DOS filesystem).
Examples:
	c.vim		perl.vim	java.vim	html.vim
	cpp.vim		sh.vim		csh.vim

The syntax file can contain any Ex commands, just like a vimrc file.  But
the idea is that only commands for a specific language are included.  When a
language is a superset of another language, it may include the other one,
for example, the cpp.vim file could include the c.vim file:
>  :so $VIM/syntax/c.vim

The .vim files are normally loaded with an autocommand.  For example:
>  :au BufNewFile,BufReadPost *.c,*.h source $VIM/syntax/c.vim
>  :au BufNewFile,BufReadPost *.cpp source $VIM/syntax/cpp.vim


MAKING YOUR OWN SYNTAX FILES				*mysyntaxfile*

When you create your own syntax files, and you want to have these
automatically used with ":syntax on", do this:

1. Create a file that contains the autocommands to load your syntax file when
   the right file name or extension is detected.  To prevent loading two
   syntax files (when the extension is used twice), first delete other
   autocommands for the same extension.  You can also include ":highlight"
   commands in this file, which override the normal highlighting (because the
   file is sourced after setting the normal highlighting).  Example:
>    augroup syntax
>    au! BufNewFile,BufReadPost *.bat
>    au  BufNewFile,BufReadPost *.bat  so ~/vim/batch.vim
>    augroup END
>    highlight Comment gui=bold
   Let's assume you write this file in "~/vim/mysyntax.vim".

2. In your .vimrc, set the "mysyntaxfile" variable to the file you just
   created.  For example:
>  :let mysyntaxfile = "~/vim/mysyntax.vim"

3. If your file type can only be detected by inspecting the contents of the
   file, create another file for doing this.  See $VIM/syntax/scripts.vim for
   examples.  Let's assume you write this file in "~/vim/myscripts.vim".
   Then set the "myscriptsfile" variable to this file name.  Example:
>  :let myscriptsfile = "~/vim/myscripts.vim"
   Note that this file is only used when no syntax file was loaded by the
   autocommands, if the file type has not been detected by the file name or
   extension.

Note that "mysyntaxfile" is sourced AFTER defining the default autocommands
for the supplied syntax files, so you can override these with your own files.
The "myscriptsfile" is loaded before the default checks for syntax files,
which also means that your rules override the supplied rules.


NAMING CONVENTIONS
							*group-name*
To be able to allow each user to pick his favorite set of colors, there need
to be preferred names for highlight groups that are common for many languages.
These are the ones that are suggested to be used:

	*Comment	any comment

	*Constant	any constant
	 String		a string constant: "this is a string"
	 Character	a character constant: 'c', '\n'
	 Number		a number constant: 234, 0xff
	 Boolean	a boolean constant: TRUE, false
	 Float		a floating point constant: 2.3e10

	*Identifier	any variable name
	 Function	function name (also: methods for classes)

	*Statement	any statement
	 Conditional	if, then, else, endif, switch, etc.
	 Repeat		for, do, while, etc.
	 Label		case, default, etc.
	 Operator	"sizeof", "+", "*", etc.
	 Keyword	any other keyword
	 Exception	try, catch, throw

	*PreProc	generic Preprocessor
	 Include	preprocessor #include
	 Define		preprocessor #define
	 Macro		same as Define
	 PreCondit	preprocessor #if, #else, #endif, etc.

	*Type		int, long, char, etc.
	 StorageClass	static, register, volatile, etc.
	 Structure	struct, union, enum, etc.
	 Typedef	A typedef

	*Special	any special symbol
	 SpecialChar	special character in a constant
	 Tag		you can use CTRL-] on this
	 Delimiter	character that needs attention
	 SpecialComment	special things inside a comment
	 Debug		debugging statements

	*Error		any erroneous construct

	*Todo		anything that needs extra attention

The ones marked with * are the preferred groups, the other are minor groups.
For the preferred groups, the "syntax.vim" file contains default highlighting.
The minor groups are linked to the preferred groups, so they get the same
highlighting.  You can override these defaults by giving ":highlight" commands
after sourcing the "syntax.vim" file.

Note that highlight group names are not case sensitive.  "String" and "string"
can be used for the same group.

The following names are reserved and cannot be used as a group name:
	NONE   ALL   ALLBUT   contains   contained

==============================================================================
3. Syntax file remarks					*:syn-file-remarks*

						*current_syntax-variable*
The name of the syntax that has been loaded is stored in the "current_syntax"
variable.  You can use this if you want to load other settings, depending on
which syntax is active.  Example:
>  :au BufReadPost * if current_syntax == "csh"
>  :au BufReadPost *   do-some-things
>  :au BufReadPost * endif


2HTML						*2html.vim* *convert-to-HTML*

This is not a syntax file itself, but a script that converts the current
window into HTML.  A new window is opened, in which the HTML file is built.
	Warning: This is slow!
The resulting file can be written where you want it.  You can then view it
with any HTML viewer, such as Netscape.  The colors should be exactly the same
as you see them in Vim.  From Netscape you can also print the file (in color)!
This only works in the GUI version.  When 'tabstop' is not 8, the amount of
white space will be wrong.


HTML							*html.vim*

The coloring scheme for tags in the html file works as follows

The  <> of opening tags are colored differently than the </> of a closing tag.
This is on purpose! For opening tags the 'Function' color is used, while for
closing tags the 'Type' color is used (See syntax.vim to check how those are
defined for you)

Known tag names are colores the same way as statements in C.
Unknown tag names are colored with the same color as the
<> or </> respectively which makes it easy to spot errors

Note that the same is true for argument (or attribute names). Known
attribute names are colored differently than unknown ones.

HTML comments are rather special (see an html reference document for the
details), and the syntax coloring scheme will highlight all errors.

JavaScript embedded inside html documents is highlighted as 'Special'
with statements, comments, strings and so on colored as in standard
programming languages. Note that only JavaScript is currently supported,
no other scripting language has been added yet.


C							*c.vim*

Most C highlighting is fixed.  There is one optional feature: highlighting
strings and numbers inside a comment.  It can be enabled by adding this line
to your vimrc:
	let c_comment_strings=1
To disable it again, use this:
	unlet c_comment_strings


JAVA							*java.vim*

The java.vim syntax highlighting file offers several options:

In Java 1.0.2 it was never possible to have braces inside parens,
so this was flagged as an error.  Since Java 1.1 this is possible (with
anonymous classes), and therefore is no longer marked as an error. If you
prefer the old way, put the following line into your vim startup file:
>  let java_mark_braces_in_parens_as_errors=1

Function names are not highlighted, as the way to find functions depends on
how you write java code.  To enable it anyway put the following line in your
startup file:
>  let java_highlight_functions=1
This will only work if you either use two spaces for indentation or tabs.
If you use another indentation style but would still want function
declarations to be highlighted create your own definitions by changing the
definitions in java.vim or by creating your own java.vim which includes
the original one and then adds the code to highlight functions.

In java 1.1 the functions System.out.println() and System.err.println() should
only be used for debugging. Therefor it is possible to highlight debugging
statements differently. To do this you must add the following definition in
your startup file:
>  let java_highlight_debug=1
The result will be that those statements are highlighted as 'Special'
characters. If you prefer to have them highlighted differently you must define
new highlightings for the following groups.:
    Debug, DebugSpecial, DebugString, DebugBoolean, DebugType
which are used for the statement itself, special characters used in debug
strings, strings, boolean constants and types (this, super) respectively. I
have opted to chose another background for those statements.

Javadoc is a program that takes special comments out of java program files and
creates html pages. The standard configuration will highlight this html code
similarly to html files (see |html.vim|). There are three differences:
  1. The title (all characters up to the first '.' which is followed by
     some white space or to the first '@') is colored differently (to change
     the color change the group CommentTitle).
  2. The text is colored as 'Comment'.
  3. HTML comments are colored as 'Special'
To turn this feature off add the following line to your startup file:
>  let java_ignore_javadoc=1


SH							*sh.vim*

This covers the "normal" Unix sh, bash and the korn shell.  If you're working
on a system where bash is called sh, you will benefit to define the vim
variable 'bash_is_sh' in your '.vimrc' file:
>  let bash_is_sh = 1

To choose between the two ways to treat single-quotes inside a pair of
double-quotes, I have introduced a Vim variable "highlight_balanced_quotes".
By default (ie by not declaring this variable) single quotes can be used
inside double quotes, and are not highlighted.  If you prefer balanced single
quotes as I do you just make the statement in your .vimrc file:
>  let highlight_balanced_quotes = 1

Similar I have introduced another vim variable "highlight_function_name" to be
used to enable/disable highlighting of the function-name in function
declaration.  Default is not to highlight the function name.  If you want to
highlight functions names, include this in your .vimrc file:
>  let highlight_function_name = 1

==============================================================================
4. Defining a syntax					*:syn-define*

Vim understands three types of syntax items:
1. A keyword.  It can only contain keyword characters, according to the
   'iskeyword' option.  It cannot contain other syntax items.  It will only
   be recognized when it is a complete match (there are no keyword
   characters before or after the match).  "if" would match in "if(a=b)",
   but not in "ifdef x".
2. A match.  This is a match with a single regexp pattern.  It must be within
   one line.
3. A region.  This starts at a match of the start regexp pattern and
   ends with a match with the end regexp pattern.  A skip regexp pattern can
   be used to avoid matching the end pattern.

Several syntax ITEMs can be put into one syntax GROUP.  For a syntax group
you can give highlighting attributes.  For example, you could have an item
to define a "/* .. */" comment and another one that defines a "// .." comment,
and put them both in the "Comment" group.  You can then specify that a
"Comment" will be in bold font and have a blue color.  You are free to make
one highlight group for one syntax item, or put all items into one group.
This depends on how you want to specify your highlighting attributes.  Putting
each item in its own group results in having to specify the highlighting
for a lot of groups.

Note that a syntax group and a highlight group are similar.  For a highlight
group you will have given highlight attributes.  These attributes will be used
for the syntax group with the same name.

In case more than one item matches at the same position, the one that was
defined LAST wins.  Thus you can override previously defined syntax items by
using an item that matches the same text.  But a keyword always goes before a
match or region.  And a keyword with matching case always goes before a
keyword with ignoring case.


DEFINING CASE						*:syn-case*

:syntax case [match|ignore]
	This defines if the following ":syntax" commands will work with
	matching case, when using "match", or with ignoring case, when using
	"ignore".  Note that any items before this are not affected, and all
	items until the next ":syntax case" command are affected.


DEFINING KEYWORDS					*:syn-keyword*

:syntax keyword {group-name} [{options}] {keyword} .. [{options}]

	This defines a number of keywords.

	{group-name}	Is a syntax group name such as "Comment".
	[{options}]	See |:syn-arguments| below.
	{keyword} ..	Is a list of keywords which are part of this group.

	Example:
>  :syntax keyword   Type   int long char

	The {options} can be given anywhere in the line.  They will apply to
	all keywords given, also for options that come after a keyword.
	These examples do exactly the same:
>  :syntax keyword   Type   contained int long char
>  :syntax keyword   Type   int long contained char
>  :syntax keyword   Type   int long char contained

	When you have a keyword with an optional tail, like Ex commands in
	Vim, you can put the optional characters inside [], to define all the
	variations at once:
>  :syntax keyword   VimCommand   ab[breviate] n[ext]

	A keyword always has higher priority than a match or region, the
	keyword is used if more than one item matches.  Keywords do not nest
	and a keyword can't contain anything else.

	Note that when you have a keyword that is the same as an option (even
	one that isn't allowed here), you can not use it.  Use a match
	instead.

    	The maximum length of a keyword is 80 characters.

	The same keyword can be defined multiple times, when its containment
	differs.  For example, you can define the keyword once not contained
	and use one highlight group, and once contained, and use a different
	highlight group. Example:
>  :syn keyword vimCommand tag
>  :syn keyword vimSetting contained tag
	When finding "tag" outside of any syntax item, the "vimCommand"
	highlight group is used.  When finding "tag" in a syntax item that
	contains "vimSetting", the "vimSetting" group is used.


DEFINING MATCHES					*:syn-match*

:syntax match {group-name} [{options}] {pattern} [{options}]

	This defines one match.

	{group-name}		A syntax group name such as "Comment".
	[{options}]		See |:syn-arguments| below.
	{pattern}		The search pattern that defines the match.
				See |:syn-pattern| below.

	Example (match a character constant):
>  :syntax match Character /'.'/s+1e-1


DEFINING REGIONS	*:syn-region* *:syn-start* *:syn-skip* *:syn-end*

:syntax region {group-name} [{options}]
		[matchgroup={group_name}]
		[keepend]
		start={start_pattern} ..
		[skip={skip_pattern}]
		end={end_pattern} ..
		[{options}]

	This defines one region.  It may span several lines.

	{group-name}		A syntax group name such as "Comment".
	[{options}]		See |:syn-arguments| below.
	[matchgroup={group-name}]  The syntax group to use for the following
				start or end pattern matches only.  Not used
				for the text in between the matched start and
				end patterns.  Use NONE to reset to not using
				a different group for the start or end match.
				See |:syn-matchgroup|.
	keepend			Don't allow contained matches to go past a
				match with the end pattern.  See
				|:syn-keepend|.
	start={start_pattern}	The search pattern that defines the start of
				the region.  See |:syn-pattern| below.
	skip={skip_pattern}	The search pattern that defines text inside
				the region where not to look for the end
				pattern.  See |:syn-pattern| below.
	end={end_pattern}	The search pattern that defines the end of
				the region.  See |:syn-pattern| below.

	Example:
>  :syntax region String   start=+"+  skip=+\\"+  end=+"+

	The start/skip/end patterns and the options can be given in any order.
	There can be zero or one skip pattern.  There must be one or more
	start and end patterns.  This means that you can omit the skip
	pattern, but you must give at least one start and one end pattern.  It
	is allowed to have white space before and after the equal sign
	(although it mostly looks better without white space).

	When more than one start pattern is given, a match with one of these
	is sufficient.  This means there is an OR relation between the start
	patterns.  The first one that matches is used.  The same is true for
	the end patterns.

	The search for the end pattern starts at the start of the region.
	This implies that it can also match inside the start pattern!

	Note: The decision to start a region is only based on a matching start
	pattern.  There is no check for a matching end pattern.  This does NOT
	work:
		:syn region First  start="("  end="."
		:syn region Second start="("  end=";"
	The Second always matches before the First (last defined pattern has
	higher priority).  The Second region then continues until the next
	';', no matter if there is a '.' before it.

							*:syn-keepend*
	By default, a contained match can obscure a match for the end pattern.
	This is useful for nesting.  For example, a region that starts with
	"{" and ends with "}", can contain another region.  An encountered "}"
	will then end the contained region, but not the outer region:
	    {		starts outer "{}" region
		{	starts contained "{}" region
		}	ends contained "{}" region
	    }		ends outer "{} region
	If you don't want this, the "keepend" argument will make the matching
	of an end pattern of the outer region also end any contained item.
	This makes it impossible to nest the same region, but allows for
	contained items to highlight parts of the end pattern, without causing
	that to skip the match with the end pattern.  Example:
>  :syn match  VimComment +"[^"]\+$+
>  :syn region VimCommand start="set" end="$" contains VimComment keepend
	The "keepend" makes the VimCommand always end at the end of the line,
	even though the contained VimComment includes a match with the <EOL>.

	When "keepend" is not used, a match with an end pattern is retried
	after each contained match.  When "keepend" is included, the first
	encountered match with an end pattern is used, truncating any
	contained matches.

							*:syn-matchgroup*
	"matchgroup" can be used to highlight the start and/or end pattern
	differently than the body of the region.  Example:
>  :syntax region String matchgroup=Quote start=+"+  skip=+\\"+  end=+"+
	This will highlight the quotes with the "Quote" group, and the text in
	between with the "String" group.
	The "matchgroup" is used for all start and end patterns that follow,
	until the next "matchgroup".  Use "matchgroup=NONE" to go back to not
	using a matchgroup.

	It is not possible to have a contained match in a start or end pattern
	that is highlighted with "matchgroup".
	When using "transparent", it does not apply to a start or end pattern
	that is highlighted with "matchgroup".

==============================================================================
5. :syntax arguments					*:syn-arguments*

The :syntax commands that define syntax items take a number of arguments.
The common ones are explained here.  The arguments may be given in any order
and may be mixed with patterns.

Not all commands accept all arguments.  This table shows which arguments
can be used for each command:

		 contained  nextgroup  skip*   transparent  contains  oneline ~
:syntax keyword	    yes	       yes	yes	    -	       -	 -
:syntax match	    yes	       yes	yes	   yes	      yes	 -
:syntax region	    yes	       yes	yes	   yes	      yes	yes


contained 						*:syn-contained*

When the "contained" argument is given, this item will not be recognized at
the top level, but only when it is mentioned in the "contains" field of
another match.  Example:
>   :syntax keyword Todo    TODO    contained
>   :syntax match   Comment "//.*"  contains=Todo


transparent						*:syn-transparent*

If the "transparent" argument is given, this item will not be highlighted
itself, but will take the highlighting of the item it is contained in.  This
is useful for syntax items that don't need any highlighting but are used
only to skip over a part of the text.  The same groups as the item it is
contained in are used, unless a "contains" argument is given too.


oneline							*:syn-oneline*

The "oneline" argument indicates that the region does not cross a line
boundary.  It must match completely in the current line.  However, when the
region has a contained item that does cross a line boundary, it continues on
the next line anyway.  A contained item can be used to recognize a line
continuation pattern.


contains={groupname},..					*:syn-contains*

The "contains" argument is followed by a list of syntax group names.  These
groups will be allowed to begin inside the item (they may extend past the
containing group's end).  This allows for recursive nesting of matches and
regions.  If there is no "contains" argument, no groups will be contained in
this item.  The group names do not need to be defined before they can be used
here.

contains=ALL
		If the only item in the contains list is "ALL", then all
		groups will be accepted inside the item.

contains=ALLBUT,{group-name},..
		If the first item in the contains list is "ALLBUT", then all
		groups will be accepted inside the item, except the ones that
		are listed, and the "contained" items.  Example:
>  :syntax region Block start="{" end="}" ... contains=ALLBUT,Function

The {group-name} in the "contains" list can be a pattern.  All group names
that match the pattern will be included (or excluded, if "ALLBUT" is used).
The pattern cannot contain white space or a ','.  Example:
>  ... contains=Comment.*,Keyw[0-3]


nextgroup={groupname},..				*:syn-nextgroup*

The "nextgroup" argument is followed by a list of syntax group names,
separated by commas (just like with "contains", so you can also use patterns).

If the "nextgroup" argument is given, the mentioned syntax groups will be
tried for a match, after the match or region ends.  If none of the groups have
a match, highlighting continues normally.  If there is a match, this group
will used, even when it is not mentioned in the "contains" field of the
current group.  This is like giving the mentioned group priority over all
other groups.  Example:
>   :syntax match  ccFoobar  "Foo.\{-}Bar"  contains=ccFoo
>   :syntax match  ccFoo     "Foo"	    contained nextgroup=ccFiller
>   :syntax region ccFiller  start="."  matchgroup=ccBar  end="Bar"  contained

This will highlight "Foo" and "Bar" differently, and only when there is a
"Bar" after "Foo".  In the text line below, "f" shows where ccFoo is used for
highlighting, and "bbb" where ccBar is used.

>   Foo asdfasd Bar asdf Foo asdf Bar asdf
>   fff		bbb      fff      bbb

Note the use of ".\{-}" to skip as little as possible until the next Bar.
when ".*" would be used, the "asdf" in between "Bar" and "Foo" would be
highlighted according to the "ccFoobar" group, because the ccFooBar match
would include the first "Foo" and the last "Bar" in the line (see |pattern|).


skipwhite						*:syn-skipwhite*
skipnl							*:syn-skipnl*
skipempty						*:syn-skipempty*

These arguments are only used in combination with "nextgroup".  They can be
used to allow the next group to match after skipping some text:
	skipwhite	skip over space and Tab characters
	skipnl		skip over the end of a line
	skipempty	skip over empty lines (implies a "skipnl")

When "skipwhite" is present, the white space is only skipped if there is no
next group that matches the white space.

When "skipnl" is present, the match with nextgroup may be found in the next
line.  This only happens when the current item ends at the end of the current
line!  When "skipnl" is not present, the nextgroup will only be found after
the current item in the same line.

When skipping text while looking for a next group, the matches for other
groups are ignored.  Only when no next group matches, other items are tried
for a match again.  This means that matching a next group and skipping white
space and <EOL>s has a higher priority than other items.

Example:
>  syn match ifstart "if.*"     nextgroup=ifline skipwhite skipempty
>  syn match ifline  "endif"    contained
>  syn match ifline  "[^ \t].*" nextgroup=ifline skipwhite skipempty contained
Note that the last match, which matches any non-white text, is put last,
otherwise the "endif" of the indent would never match, because the "[^ \t].*"
would match first.
Note that this example doesn't work for nested "if"s.  You need to add
"contains" arguments to make that work (omitted for simplicity of the
example).

==============================================================================
6. Syntax patterns					*:syn-pattern*

In the syntax commands, a pattern must be surrounded by two identical
characters.  This is like it works for the ":s" command.  The most common to
use is the double quote.  But if the pattern contains a double quote, you can
use another character that is not used in the pattern.  Examples:
>  :syntax region Comment  start="/\*"  end="\*/"
>  :syntax region String   start=+"+    end=+"+   skip=+\\"+

See |pattern| for the explanation of what a pattern is.  Syntax patterns are
always interpreted like the 'magic' options is set, no matter what the actual
value of 'magic' is.  And the patterns are interpreted like the 'l' flag is
not included in 'cpoptions'.  This was done to make syntax files portable and
independent of 'compatible' and 'magic' settings.

Try to avoid patterns that can match an empty string, such as "[a-z]*".
This slows down the highlighting a lot, because it matches everywhere.

The pattern can be followed by a character offset.  This can be used to
change the highlighted part, and to change the text area included in the
match or region (which only matters when trying to match other items).  Both
are relative to the matched pattern.  The character offset for a skip
pattern can be used to tell where to continue looking for an end pattern.

The offset takes the form of "{what}={offset}"
The {what} can be one of six strings:

ms	Match Start	offset for the start of the matched text
me	Match End	offset for the end of the matched text
hs	Highlight Start	offset for where the highlighting starts
he	Highlight End	offset for where the highlighting ends
rs	Region Start	offset for where the body of a region starts
re	Region End	offset for where the body of a region ends
lc	Leading Context	offset past "leading context" of pattern

The {offset} can be:

s	start of the matched pattern
s+{nr}	start of the matched pattern plus {nr} chars to the right
s-{nr}	start of the matched pattern plus {nr} chars to the left
e	end of the matched pattern
e+{nr}	end of the matched pattern plus {nr} chars to the right
e-{nr}	end of the matched pattern plus {nr} chars to the left
{nr}	(for "lc" only): start matching {nr} chars to the left

Examples: "ms=s+1", "hs=e-2", "lc=3".

Although all offsets are accepted after any pattern, they are not always
meaningful.  This table shows which offsets are actually used:

		    ms   me   hs   he   rs   re	  lc ~
match item	    yes  yes  yes  yes  -    -    yes
region item start   yes  -    yes  -    yes  -    yes
region item skip    -    yes  -    -    -    -    -
region item end     -    yes  -    yes  -    yes  -

Offsets can be concatenated, with a ',' in between.  Example:
>  syn match String  /".*"/hs=s+1,he=e-1

    some "string" text
	  ^^^^^^		highlighted

Notes:
- There must be no white space between the pattern and the character
  offset(s).
- The highlighted area will never be outside of the matched text.
- A negative offset for an end pattern may not always work, because the end
  pattern may be detected when the highlighting should already have stopped.

Example (match a comment but don't highlight the /* and */):
>  :syntax region Comment start="/\*"hs=e+1 end="\*/"he=s-1

	/* this is a comment */
	  ^^^^^^^^^^^^^^^^^^^     highlighted

A more complicated Example:
>  :syn region Exa matchgroup=Foo start="foo"hs=s+2,rs=e+2 matchgroup=Bar end="bar"me=e-1,he=e-1,re=s-1

	 abcfoostringbarabc
	    mmmmmmmmmmm	    match
	      ssrrrreee	    highlight start/region/end ("Foo", "Exa" and "Bar")

Leading context			*:syn-lc* *:syn-leading* *:syn-context*

The "lc" offset specifies leading context -- a part of the pattern that must
be present, but is not considered part of the match.  An offset of "lc=n" will
cause Vim to step back n columns before attempting the pattern match, allowing
characters which have already been matched in previous patterns to also be
used as leading context for this match.  This can be used, for instance, to
specify that an "escaping" character must not precede the match:

>  :syn match ZNoBackslash "[^\\]z"ms=s+1
>  :syn match WNoBackslash "[^\\]w"lc=1
>  :syn match Underline "_\+"

	  ___zzzz ___wwww
	  ^^^     ^^^	  matches Underline
	      ^ ^	  matches ZNoBackslash
		     ^^^^ matches WNoBackslash

The "ms" offset is automatically set to the same value as the "lc" offset,
unless you set "ms" explicitly.

==============================================================================
7. Synchronizing					*:syn-sync*

Vim wants to be able to start redrawing in any position in the document.  To
make this possible it needs to know the syntax item at the position where
redrawing starts.

:syntax sync [ccomment [group-name] | minlines={N} | ...]

There are three ways to synchronize:
1. Based on C-style comments.  Vim understands how C-comments work and can
   figure out if the current line starts inside or outside a comment.
2. Jumping back a certain number of lines and start parsing there.
3. Searching backwards in the text for a pattern to sync on.

For all three methods, the line range where the parsing can start is limited
by "minlines" and "maxlines".

If the "minlines={N}" argument is given, the parsing always starts at least
that many lines backwards.  This can be used if the parsing may take a few
lines before it's correct, or when it's not possible to use syncing.

If the "maxlines={N}" argument is given, the number of lines that are searched
for a comment or syncing pattern is restricted to N lines backwards (after
adding "minlines".  This is useful if you have few things to sync on and a
slow machine.  Example:
>  :syntax sync ccomment maxlines=500


First syncing method:

For the first method, only the "ccomment" argument needs to be given.
Example:
>  :syntax sync ccomment

When Vim finds that the line where displaying starts is inside a C-style
comment, the first region syntax item with the group-name "Comment" will be
used.  This requires that there is a region with the group-name "Comment"!
An alternate group name can be specified, for example:
>  :syntax sync ccomment javaComment

The "maxlines" argument can be used to restrict the search to a number of
lines.  The "minlines" argument can be used to at least start a number of
lines back (e.g., for when there is some construct that only takes a few
lines, but it hard to sync on).

Note: Syncing on a C comment doesn't work properly when strings are used
that cross al line and contain a "*/".  Since letting strings cross a line
is a bad programming habit (many compilers give a warning message), and the
chance of a "*/" appearing inside a comment is very small, this restriction
is hardly ever noticed.


Second syncing method:

For the second method, only the "lines={N}" argument needs to be given.  Vim
will subtract {N} from the line number and start parsing there.  This means
{N} extra lines need to be parsed, which makes this method a bit slower.
Example:
>  :syntax sync lines=50

"lines" and "minlines" are equivalent.


Third syncing method:

The idea is to synchronize on the end of a few specific regions, called a
sync pattern.  Only regions can cross lines, so when we find the end of some
region, we might be able to know in which syntax item we are.  The search
starts in the line just above the one where redrawing starts.  From there
the search continues backwards in the file.

This works just like the non-syncing syntax ltems.  You can use contained
matches, nextgroup, etc.  But there are a few differences:
- Keywords cannot be used.
- The syntax items with the "sync" keyword form a completely separated group
  of syntax items.  You can't mix syncing groups and non-syncing groups.
- The matching works backwards in the buffer (line by line), instead of
  forwards.
- A line continuation pattern can be given.  It is used to decide which group
  of lines need to be searched like they were one line.  This means that the
  search for a match with the specified items starts in the first of the
  consecutive that contain the continuation pattern.
- When using "nextgroup" or "contains", this only works within one line (or
  group of continuated lines).
- When a match with a sync pattern is found, the rest of the line (or group of
  continuated lines) is searched for another match.  The last match is used.
  This is used when a line can contain both the start end the end of a region
  (e.g., in a C-comment like /* this */, the last "*/" is used).

There are two ways how a match with a sync pattern can be used:
1. Parsing for highlighting starts where redrawing starts (and where the
   search for the sync pattern started).  The syntax group that is expected
   to be valid there must be specified.  This works well when the regions
   that cross lines cannot contain other regions.
2. Parsing for highlighting continues just after the match.  The syntax group
   that is expected to be present just after the match must be specified.
   This can be used when the previous method doesn't work well.  It's much
   slower, because more text needs to be parsed.
Both types of sync patterns can be used at the same time.

Besides the sync patterns, other matches and regions can be specified, to
avoid finding unwanted matches.

[The reason that the sync patterns are given separately, is that mostly the
search for the sync point can be much simpler than figuring out the
highlighting.  The reduced number of patterns means it will go (much)
faster.]

    :syntax sync match {group-name} grouphere {sync-group-name} ..

	Define a match that is used for syncing.  {sync-group-name} is the
	name of a syntax group that follows just after the match.  Parsing
	of the text for highlighting starts just after the match.  A region
	must exist for this sync-group-name.  The first one defined will be
	used.  "NONE" can be used for when there is no syntax group after the
	match.

    :syntax sync match {group-name} groupthere {sync-group-name} ..

	Like "grouphere", but {sync-group-name} is the name of a syntax
	group that is to be used at the start of the line where searching
	for the sync point started.  The text between the match and the
	start of the sync pattern searching is assumed not to change the
	syntax highlighting.  For example, in C you could search backwards for
	"/*" and "*/".  If "/*" is found first, you know that you are inside a
	comment, so the "groupthere" is "cComment".  If "*/" is found first,
	you know that you are not in a comment, so the "groupthere" is "NONE".
	(in practice it's a bit more complicated, because the "*/" and "*/"
	could appear inside a string.  That's left as an exercise to the
	reader...).

    :syntax sync match ..
    :syntax sync region ..

	Without a "groupthere" argument.  Define a region or match that is
	skipped while searching for a sync point.

    :syntax sync linecont {pattern}

	When {pattern} matches in a line, it is considered to continue in
	the next line.  This means that the search for a sync point will
	consider the lines to be concatenated.

If the "maxlines={N}" argument is given too, the number of lines that are
searched for a match is restricted to N.  This is useful if you have very
few things to sync on and a slow machine.  Example:
>  :syntax sync maxlines=100

You can clear all sync settings with:
>  :syntax sync clear

You can clear specific sync patterns with:
>  :syntax sync clear {group-name} ..

==============================================================================
8. Listing syntax items					*:syntax* *:sy* *:syn*

This commands lists all the syntax items:

    :syntax [list]

To show the syntax items for one syntax group:

    :syntax list {group-name}

See above for other arguments for the ":syntax" command.

Note that the ":syntax" command can be abbreviated to ":sy", although ":syn"
is mostly used, because it looks better.

==============================================================================
9. Highlight command					*:highlight*

There are two types of highlight groups:
- The ones used for specific languages.  For these the name starts with the
  name of the language.  Many of these don't have any attributes, but are
  linked to a group of the second type.
- The ones used for all languages.  These are also used for the 'highlight'
  option.

:highlight		List all the current highlight groups that have
			attributes set.

:highlight {group-name}
			List one highlight group.

:highlight clear {group-name}
:highlight {group-name} NONE
			Disable the highlighting for one highlight group.

:highlight {group-name} {key}={arg} ..
			Add a highlight group, or change the highlighting for
			an existing group.  See below for the arguments
			|highlight-args|.

Normally a highlight group is added once, in the *.vim file.  This sets
the default values for the highlighting.  After that, you can use additional
highlight commands to change the arguments that you want to set to
non-default values.  The value "NONE" can be used to switch the value off or
go back to the default value.

Example.  The syntax.vim file contains this line:
>  hi Comment	term=bold ctermfg=Cyan guifg=#80a0ff

You can change this by giving another ":highlight: command:
>  hi Comment	gui=bold

Note that all settings that are not included remain the same, only the
specified field is used, and settings are merged with previous ones.  So, the
result is like this single command has been used:
>  hi Comment	term=bold ctermfg=Cyan guifg=#80a0ff gui=bold

						*highlight-args*
There are three types of terminals for highlighting:
term	a normal terminal (vt100, xterm)
cterm	a color terminal (MS-DOS console, color-xterm, these have the "Co"
	termcap entry)
gui	the GUI

For each type the highlighting can be given.  This makes it possible to use
the same syntax file on all terminals, and use the optimal highlighting.

1. highlight arguments for normal terminals

term={attr-list}				*attr-list* *highlight-term*
	attr-list is a comma separated list (without spaces) of the
	following items (in any order):
		bold
		underline
		reverse
		inverse		same as reverse
		italic
		standout
		NONE		no attributes used (used to reset it)

	Note that "bold" can be used here and by using a bold font.  They
	have the same effect.

start={term-list}				*highlight-start*
stop={term-list}				*term-list* *highlight-stop*
	These lists of terminal codes can be used to get
	non-standard attributes on a terminal.

	The escape sequence specified with the "start" argument
	is written before the characters in the highlighted
	area.  It can be anything that you want to send to the
	terminal to highlight this area.  The escape sequence
	specified with the "stop" argument is written after the
	highlighted area.  This should undo the "start" argument.
	Otherwise the screen will look messed up.

	The {term-list} can have two forms:

	1. A string with escape sequences.
	   This is any string of characters, except that it can't start with
	   "t_" and blanks are not allowed.  The <> notation is recognized
	   here, so you can use things like "<Esc>" and "<Space>".  Example:
		start=<Esc>[27h;<Esc>[<Space>r;

	2. A list of terminal codes.
	   Each terminal code has the form "t_xx", where "xx" is the name of
	   the termcap entry.  The codes have to be separated with commas.
	   White space is not allowed.  Example:
		start=t_C1,t_BL
	   The terminal codes must exist for this to work.


2. highlight arguments for color terminals

cterm={attr-list}					*highlight-cterm*
	See above for the description of {attr-list} |attr-list|.
	The "cterm" argument is likely to be different from "term", when
	colors are used.  For example, in a normal terminal comments could
	be underlined, in a color terminal they can be made Blue.
	Note: Many terminals (e.g., DOS console) can't mix these attributes
	with coloring.  Use only one of "cterm=" OR "ctermfg=" OR "ctermbg=".

ctermfg={color-nr}					*highlight-ctermfg*
ctermbg={color-nr}					*highlight-ctermbg*
	The {color-nr} argument is a color number.  Its range is zero to
	(not including) the number given by the termcap entry "Co".
	The actual color with this number depends on the type of terminal
	and its settings.  Sometimes the color also depends on the settings of
	"cterm".  For example, on some systems "cterm=bold ctermfg=3" gives
	another color, on others you just get color 3.

	For an xterm this depends on your resources, and is a bit
	unpredictable.  See your xterm documentation for the defaults.  The
	colors for a color-xterm can be changed from the .Xdefaults file.
	Unfortunately this means that it's not possible to get the same colors
	for each user.  See |xterm-color| for info about color xterms.

	The MSDOS standard colors are fixed (in a console window), so these
	have been used for the names.  But the meaning of color names in X11
	are fixed, so these color settings have been used, to make the
	highlighting settings portable (complicated, isn't it?).  The
	following names are recognized, with the color number used:

	    NR-16   NR-8    COLOR NAME ~
							*cterm-colors*
	    0	    0	    Black
	    1	    4	    DarkBlue
	    2       2	    DarkGreen
	    3       6	    DarkCyan
	    4       1	    DarkRed
	    5       5	    DarkMagenta
	    6       3	    Brown
	    7       7	    LightGray, LightGrey, Gray, Grey
	    8	    0*	    DarkGray, DarkGrey
	    9	    4*	    Blue, LightBlue
	    10	    2*	    Green, LightGreen
	    11	    6*	    Cyan, LightCyan
	    12	    1*	    Red, LightRed
	    13	    5*	    Magenta, LightMagenta
	    14	    3*	    Yellow
	    15	    7*	    White

	The number under "NR-16" is used for 16-color terminals ('t_Co'
	greater than or equal to 16).  The number under "NR-8" is used for
	8-color terminals ('t_Co' less than 16).  The '*' indicates that the
	bold attribute is set for ctermfg.  In many 8-color terminals (e.g.,
	"linux"), this causes the bright colors to appear.  This doesn't work
	for background colors!  Without the '*' the bold attribute is removed.
	If you want to set the bold attribute in a different way, put a
	"cterm=" argument AFTER the "ctermfg=" or "cterbg=" argument.  Or use
	a number instead of a color name.

	The case of the color names is ignored.

	Note that for some color terminals these names may result in the wrong
	colors!

	When setting the "ctermfg" or "ctermbg" colors for the Normal group,
	these will become the colors used for the non-highlighted text.
	When setting the "ctermbg" color for the Normal group, the
	'background' option will be adjusted automatically.  This causes the
	highlight groups that depend on 'background' to change!  This means
	you should set the colors for Normal first, before setting other
	colors.

	When you have set "ctermfg" or "ctermbg" for the Normal group, Vim
	needs to reset the color when exiting.  This is done with the "op"
	termcap entry |t_op|.  If this doesn't work correctly, try setting the
	't_op' option in your .vimrc.


3. highlight arguments for the GUI

gui={attr-list}						*highlight-gui*
	These give the attributes to use in the GUI mode.
	See |attr-list| for a description.
	Note that "bold" can be used here and by using a bold font.  They
	have the same effect.

font={font-name}					*highlight-font*
	font-name is the name of a font, as it is used on the system Vim
	runs on.  For X11 this is a complicated name, for example:
>  font=-misc-fixed-bold-r-normal--14-130-75-75-c-70-iso8859-1

	The font-name "NONE" can be used to revert to the default font.
	When setting the font for the "Normal" group, this becomes the default
	font (until the 'guifont' option is changed; the last one set is
	used).  All fonts used should be of the same character size as the
	default font!  Otherwise redrawing problems will occur.

guifg={color-name}					*highlight-guifg*
guibg={color-name}					*highlight-guibg*
	These give the foreground (guifg) and background (guibg) color to
	use in the GUI.  There are a few special names:
		NONE		no color (transparant)
		bg		use normal background color
		background	use normal background color
		fg		use normal foreground color
		foreground	use normal foreground color
	To use a color name with an embedded space or other special character,
	put it in single quotes.  The single quote cannot be used then.
	Example:
>	    :hi comment guifg='salmon pink'

							*gui-colors*
	Suggested color names (these are available on most systems):
	    Red		LightRed	DarkRed
	    Green	LightGreen	DarkGreen	SeaGreen
	    Blue	LightBlue	DarkBlue	SlateBlue
	    Cyan	LightCyan	DarkCyan
	    Magenta	LightMagenta	DarkMagenta
	    Yellow	LightYellow	Brown
	    Gray	LightGray	DarkGray
	    Black	White
	    Orange	Purple		Violet

	In the Win32 GUI version, additional system colors are available.  See
	|win32-colors|.

	You can also specify a color by its Red, Green and Blue values.
	The format is "#rrggbb", where
		"rr"	is the Red value
		"bb"	is the Blue value
		"gg"	is the Green value
	All values are hexadecimal, range from "00" to "ff".  Examples:
>  :highlight Comment guifg=#11f0c3 guibg=#ff00ff

							*highlight-default*
These are the default highlighting groups.  These groups are used by the
'highlight' option default.  See |'highlight'| for where each group is used.
    Cursor	guibg=Green				    *hl-Cursor*
    		Highlighting for the character under the cursor.
    Directory   term=bold ctermfg=Blue guifg=Blue	    *hl-Directory*
    IncSearch   links to |hl-Visual|	    		    *hl-IncSearch*
    ModeMsg	term=bold cterm=bold gui=bold		    *hl-ModeMsg*
							    *hl-MoreMsg*
    MoreMsg	term=bold cterm=bold ctermfg=Green gui=bold guifg=Green
							    *hl-Question*
    Question    term=standout cterm=bold ctermfg=Green gui=bold guifg=Green
    SpecialKey  term=bold ctermfg=Blue guifg=Blue	    *hl-SpecialKey*
    StatusLine  term=reverse cterm=reverse gui=reverse	    *hl-StatusLine*
							    *hl-Title*
    Title	term=bold cterm=bold ctermfg=Blue gui=bold guifg=Blue
    Visual	term=reverse cterm=reverse gui=reverse	    *hl-Visual*
    WarningMsg  term=standout ctermfg=Red guifg=Red	    *hl-WarningMsg*
    NonText	term=bold cterm=bold gui=bold guifg=Blue    *hl-NonText*

When 'background' is set to "light", these groups are used additionally:
    Normal	term=NONE cterm=NONE gui=NONE		    *hl-Normal*
    		Highlighting for the normal text.
    LineNr	term=underline ctermfg=Brown guifg=Brown    *hl-LineNr*
    ErrorMsg    term=standout ctermbg=LightRed guibg=Orange *hl-ErrorMsg*
    Search	term=reverse ctermbg=Yellow guibg=Yellow    *hl-Search*

When 'background' is set to "dark", these groups are used additionally:
    Normal	term=NONE cterm=NONE guibg=Black guifg=White *hl-Normal-dark*
    LineNr	term=underline ctermfg=Yellow guifg=Yellow   *hl-LineNr-dark*
    ErrorMsg    term=standout ctermbg=LightRed ctermfg=Black
    				guibg=Orange guifg=Black    *hl-ErrorMsg-dark*
    Search	term=reverse ctermbg=Yellow ctermfg=Yellow
    				guibg=Yellow guifg=Black    *hl-Search-dark*

For the GUI you can use these groups to set the colors for the menu and
scrollbars.  They don't have defaults.  This doesn't work for the Win32 GUI.
    Menu						    *hl-Menu*
    Scrollbar						    *hl-Scrollbar*

==============================================================================
10. Linking groups					*:highlight-link*

When you want to use the same highlighting for several syntax groups, you
can do this more easily by linking the groups into one common highlight
group, and give the color attributes only for that group.

    :highlight[!] link {from-group} {to-group}

Notes:
- If the {from-group} and/or {to-group} doesn't exist, it is created.  You
  don't get an error message for a non-existing group.
- If the {to-group} is "NONE", the link is removed from the {from-group}.
- As soon as you use a ":highlight" command for a linked group, the link is
  removed.
- If there are already highlight settings for the {from-group}, the link is
  not made, unless the '!' is given.  For a ":highlight link" command in a
  sourced file, you don't get an error message.  This can be used to skip
  links for groups that already have settings.

==============================================================================
11. Cleaning up						*:syn-clear*

If you want to clear the syntax stuff for the current buffer, you can use this
command:
>  :syntax clear

This command should be used when you want to switch off syntax highlighting,
or when you want to switch to using another syntax.  It's a good idea to
include this command at the beginning of a syntax file.

If you want to disable syntax highlighting for all buffers, you need to remove
the autocommands that load the syntax files:
>  :syntax off

What this command actually does, is executing the command
>  source $VIM/syntax/nosyntax.vim
See the "nosyntax.vim" file for details.  Note that for this to work $VIM must
be valid.  See |$VIM|.

To clean up specific syntax groups for the current buffer:
>  :syntax clear {group-name} ..
This removes all patterns and keywords for {group-name}.

==============================================================================
12. Highlighting tags					*tag-highlight*

If you want to highlight all the tags in your file, you can use the following
mappings.

	<F11>	-- Generate tags.vim file, and highlight tags.
	<F12>	-- Just highlight tags based on existing tags.vim file.

>  map <F11>  :sp tags<CR>:%s/^\([^ 	:]*:\)\=\([^ 	]*\).*/syntax keyword Tag \2/<CR>:wq! tags.vim<CR>/^<CR><F12>
>  map <F12>  :so tags.vim<CR>

WARNING: The longer the tags file, the slower this will be, and the more
memory Vim will consume.

Only highlighting typedefs, unions and structs can be done too.  For this you
must use Exhuberant ctags (included with Vim).

Put these lines in your Makefile:

# Make a highlight file for types.  Requires Exhuberant ctags and awk
types: types.vim
types.vim: *.[ch]
	ctags -i=gstuS -o- *.[ch] |\
		awk 'BEGIN{printf("syntax keyword Type\t")}\
			{printf("%s ", $$1)}END{print ""}' > $@

And put these lines in your .vimrc:

>  " load the types.vim highlighting file, if it exists
>  autocmd BufRead,BufNewFile *.[ch] let fname = expand('<afile>:p:h') . '/types.vim'
>  autocmd BufRead,BufNewFile *.[ch] if file_readable(fname)
>  autocmd BufRead,BufNewFile *.[ch]   exe 'so ' . fname
>  autocmd BufRead,BufNewFile *.[ch] endif

==============================================================================
13. Color xterms				*xterm-color* *color-xterm*

Most color xterms have only eight colors.  They should work with these
settings for without |+terminfo|:
>  :set t_Co=8
>  :set t_Sf=<Esc>[3%dm
>  :set t_Sb=<Esc>[4%dm
And with |+terminfo|:
>  :set t_Co=8
>  :set t_Sf=<Esc>[3%p1%dm
>  :set t_Sb=<Esc>[4%p1%dm
	[<Esc> is a real escape, type CTRL-V <Esc>]

To test your color setup, a file has been included in the Vim distribution.
To use it, execute these commands:
>  :e $VIM/syntax/colortest.vim
>  :so %

Some versions of xterm (and other terminals, like the linux console) can
output lighter foreground colors, even though the number of colors is defined
at 8.  Therefore Vim sets the "cterm=bold" attribute for light foreground
colors, when 't_Co' is 8.

To get 16 colors, get the newest xterm version (which should be included with
Xfree86 3.3).  You can also find the latest version at:
	http://www.clark.net/pub/dickey/xterm
You probably have to enable 16 colors when running configure:
	./configure --disable-bold-color
If you only get 8 colors, check the xterm compilation settings.

This xterm should work with these settings without |+terminfo|:
>  :set t_Co=16
>  :set t_Sf=<Esc>[3%dm
>  :set t_Sb=<Esc>[4%dm
Vim will recognize these settings, and automatically translate cterm colors of
8 and above to "<Esc>[9%dm" and "<Esc>[10%dm".

When the |+terminfo| feature is used, these settings should work:
>  :set t_Co=16
>  :set t_AB=<Esc>[%?%p1%{8}%<%t%p1%{40}%+%e%p1%{92}%+%;%dm
>  :set t_AF=<Esc>[%?%p1%{8}%<%t%p1%{30}%+%e%p1%{82}%+%;%dm
	[<Esc> is a real escape, type CTRL-V <Esc>]

Or just set the TERM environment variable to "xterm-16color" and try if that
works.

You probably want to use these X resouces (put them in your ~/.Xdefaults file):
	XTerm*color0:			#000000
	XTerm*color1:			#c00000
	XTerm*color2:			#008000
	XTerm*color3:			#808000
	XTerm*color4:			#0000c0
	XTerm*color5:			#c000c0
	XTerm*color6:			#008080
	XTerm*color7:			#c0c0c0
	XTerm*color8:			#808080
	XTerm*color9:			#ff6060
	XTerm*color10:			#00ff00
	XTerm*color11:			#ffff00
	XTerm*color12:			#8080ff
	XTerm*color13:			#ff40ff
	XTerm*color14:			#00ffff
	XTerm*color15:			#ffffff
	Xterm*cursorColor:		Black

[Note: The cursorColor is required to work around a bug, which changes the
cursor color to the color of the last drawn text.  This has been fixed by a
newer version of xterm, but not everybody is using yet]

To get these right away, reload the .Xdefaults file to the X Option database
Manager (you only need to do this when you just changed the .Xdefaults file):
>  xrdb -merge ~/.Xdefaults

 vim:tw=78:ts=8:sw=4
